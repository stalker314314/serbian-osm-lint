#defaultView:Editor{ "comment":"Poluautomatsko dodavanje name:sr-Latn na ulice u Srbiji", "vote":false }
SELECT
  ?id ?name ?loc
  ('name:sr' AS ?tag_1) (?fixed as ?val_1)
WHERE {
  ?id osmt:name ?name ;
      osmt:highway ?highway ; # Ovim se filtriraju samo ulice. Zakomentarisati za sve entitete
      osmm:type ?mtype .

  # Ovako filtriramo samo latinicna imena. Primetiti da nedostaju slova "i" i "v". To je zato sto su cesta u rimskim brojevima, pa mozemo da ih izostavimo
  # Gubimo ovako detekciju latinicnih imena "iii", "vi", "viv", ali to je OK cena za platiti:)
  FILTER (regex(str(?name), "a|b|c|č|ć|d|đ|e|f|g|h|j|k|l|m|n|o|p|r|s|š|t|u|z|ž", "i")) .
  FILTER (!regex(str(?name), "x|y|w|q", "i")) . # Ovako filtriramo ocigledno engleska imena
  # FILTER (?mtype = 'w') . # Stavite 'n', 'w' ili 'r' ako zelite da filtrirate po tipu entiteta

  # Prvo duple
  BIND(
    REPLACE(REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (?name, "DŽ", "Џ"), "Dž", "Џ"), "NJ", "Њ"), "Nj", "Њ"), "LJ", "Љ"), "dž", "џ"), "nj", "њ"), "lj", "љ")
   AS ?dupla)
  # Mala slova
  BIND(
    REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (
    REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (
    REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (?dupla,
      "a", "а"), "b", "б"), "v", "в"), "g", "г"), "d", "д"), "đ", "ђ"), "e", "е"), "ž", "ж"), "z", "з"), "i", "и"),
      "j", "ј"), "k", "к"), "l", "л"), "m", "м"), "n", "н"), "o", "о"), "p", "п"), "r", "р"), "s", "с"), "t", "т"),
      "ć", "ћ"), "u", "у"), "f", "ф"), "h", "х"), "c", "ц"), "č", "ч"), "š", "ш")
  AS ?mala)
  # Velika slova
  BIND(
    REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (
    REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (
    REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (REPLACE (?mala,
    "A", "А"), "B", "Б"), "V", "В"), "G", "Г"), "D", "Д"), "Đ", "Ђ"), "E", "Е"), "Ž", "Ж"), "Z", "З"), "I", "И"),
    "J", "Ј"), "K", "К"), "L", "Л"), "M", "М"), "N", "Н"), "O", "О"), "P", "П"), "R", "Р"), "S", "С"), "T", "Т"),
    "Ć", "Ћ"), "U", "У"), "F", "Ф"), "H", "Х"), "C", "Ц"), "Č", "Ч"), "Š", "Ш")
  AS ?fixed)

  SERVICE wikibase:around {
    ?id osmm:loc ?loc .
    # Koordinata zapadno-centralnog dela Srbije (zameni da dobijes krug sa centrom u drugoj tacki)
    bd:serviceParam wikibase:center "Point(22.10607 42.57887)"^^geo:wktLiteral.
    # Precnik kruga u kilometrima, povecaj/smanji po zelji
    bd:serviceParam wikibase:radius "31" .
  }
}